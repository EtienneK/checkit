buildscript {
  repositories {
    jcenter { url "http://jcenter.bintray.com/" }
    mavenCentral()
    maven { url 'http://repo.spring.io/milestone' }
  }
  dependencies {
    classpath("org.springframework.boot:spring-boot-gradle-plugin:1.3.1.RELEASE")
    classpath("org.batcha.gradle.plugins:git-dependencies:0.2")
  }
}

apply plugin: 'java'
apply plugin: 'spring-boot'
apply plugin: 'eclipse'
apply plugin: 'git-dependencies'

group = "com.etiennek.check"
version = "0.0.1"
sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

repositories { 
  maven { 
    url "http://repo1.maven.org/maven2"
  } 
}

ext {
  rxVersion = '1.0.16'
  guavaVersion = '19.0'
  gsonVersion = '2.5'
  awaitilityVersion = '1.7.0'
  jsoupVersion = '1.8.3'
  commonsLangVersion = '3.4'
}

dependencies {
  // Spring
  compile "org.springframework.boot:spring-boot-starter-web"
  compile "org.springframework.boot:spring-boot-starter-thymeleaf"
  compile "org.springframework.boot:spring-boot-starter-data-jpa"

  // Other
  compile "com.google.guava:guava:$guavaVersion"
  compile "com.google.code.gson:gson:$gsonVersion"
  compile "io.reactivex:rxjava:$rxVersion"
  compile "org.hsqldb:hsqldb"
  compile "org.jsoup:jsoup:$jsoupVersion"
  compile "org.apache.commons:commons-lang3:$commonsLangVersion"

  // Test
  testCompile "com.jayway.awaitility:awaitility:$awaitilityVersion"
  testCompile "org.springframework.boot:spring-boot-starter-test"
}

task wrapper(type: Wrapper) {
	gradleVersion = '2.10'
}

task stage(type: Copy, dependsOn: [clean, build]) {
    from jar.archivePath
    into project.rootDir 
    rename {
        'app.jar'
    }
}
stage.mustRunAfter(clean)

clean << {
    project.file('app.jar').delete()
}

